<html><head></head><script type="text/javascript" src="toggledisplay.js"></script><body><header><h1>cl-synthesizer-patches</h1>Example patches for cl-synthesizer. Work in progress...<p>Back to the <a href="https://frechmatz.github.io/cl-synthesizer/">project site.</a></p></header><section><h1 id="<p>440Hz sine</p>-1"><p>440Hz sine</p></h1><p><audio controls preload="none">Your browser does not support the <code>audio</code> element.<source src="sine.wav?cb=3793636011" type="audio/wav"></audio></p><p><a href="#" id="G568"onclick="return toggleDisplay('Show patch', 'Hide patch', 'G568', 'G569')">Show patch</a></p><div style="display: none" id='G569'><p><pre><code>(defpackage :cl-synthesizer-patches-sine
  (:documentation "440Hz sine")
  (:use :cl))

(in-package :cl-synthesizer-patches-sine)

(defun example ()
  (let ((rack (cl-synthesizer:make-rack
               :environment (cl-synthesizer:make-environment)
               :output-sockets '(:sine))))
    
    (cl-synthesizer:add-module
     rack
     "VCO"
     #'cl-synthesizer-modules-vco:make-module
     :base-frequency 440.0 :v-peak 5.0)

    (cl-synthesizer:add-patch rack "VCO" :sine "OUTPUT" :sine)
    
    ;; We do not want to have a hard wired Wave-Writer module in our rack.
    ;; Let the file writing stuff be handled by a monitor.
    (cl-synthesizer-monitor:add-monitor
     rack
     #'cl-synthesizer-monitor-wave-handler:make-backend
     '(("VCO" :output-socket :sine))
     :filename "src/lisp/cl-synthesizer/docs/sine.wav")

    rack))

(defparameter *duration-seconds* 3.0)
;; parameter and corresponding OUTPUT patches only required for experimental audio output
(defparameter *audio-output-sockets* '(:sine))

(defun run-example ()
  (let ((rack (example)))
    (cl-synthesizer:play-rack rack :duration-seconds *duration-seconds*)))

;; (run-example)</code></pre></p></div><h1 id="<p>Two frequency modulated saws</p>-2"><p>Two frequency modulated saws</p></h1><p><audio controls preload="none">Your browser does not support the <code>audio</code> element.<source src="siren.wav?cb=3793636011" type="audio/wav"></audio></p><p><a href="#" id="G570"onclick="return toggleDisplay('Show patch', 'Hide patch', 'G570', 'G571')">Show patch</a></p><div style="display: none" id='G571'><p><pre><code>(defpackage :cl-synthesizer-patches-siren
  (:use :cl)
  (:documentation "Two frequency modulated saws"))

(in-package :cl-synthesizer-patches-siren)

(defun make-voice (name environment &amp;key lfo-frequency vco-frequency)
  "Frequency modulated saw"
  (declare (ignore name))
  (let ((voice
         (cl-synthesizer:make-rack
          :environment environment
          :output-sockets '(:audio))))
    
    (cl-synthesizer:add-module
     voice "LFO"
     #'cl-synthesizer-modules-vco:make-module
     :base-frequency lfo-frequency :v-peak 5.0)

    (cl-synthesizer:add-module
     voice "VCO"
     #'cl-synthesizer-modules-vco:make-module
     :base-frequency vco-frequency :v-peak 5.0 :cv-lin-hz-v 20.0)

    (cl-synthesizer:add-patch voice "LFO" :sine "VCO" :cv-lin)
    (cl-synthesizer:add-patch voice "VCO" :saw "OUTPUT" :audio)
    
    voice))
  
(defun example ()
  (let ((rack (cl-synthesizer:make-rack
               :environment (cl-synthesizer:make-environment)
               :output-sockets '(:left :right))))

    (cl-synthesizer:add-module
     rack "VOICE-1" #'make-voice :lfo-frequency 1.0 :vco-frequency 440.0)
    (cl-synthesizer:add-module
     rack "VOICE-2" #'make-voice :lfo-frequency 2.0 :vco-frequency 442.0)

    (cl-synthesizer:add-patch rack "VOICE-1" :audio "OUTPUT" :left)
    (cl-synthesizer:add-patch rack "VOICE-2" :audio "OUTPUT" :right)

    (cl-synthesizer-monitor:add-monitor
     rack
     #'cl-synthesizer-monitor-wave-handler:make-backend
     '(("VOICE-1" :output-socket :audio)
       ("VOICE-2" :output-socket :audio))
     :filename "src/lisp/cl-synthesizer/docs/siren.wav")
    
    rack))

(defparameter *duration-seconds* 2.0)
;; parameter and corresponding OUTPUT patches only required for experimental audio output
(defparameter *audio-output-sockets* '(:left :right))

(defun run-example ()
  (cl-synthesizer:play-rack
   (example)
   :duration-seconds *duration-seconds*))
  
;;(run-example)</code></pre></p></div></section><footer><p><small>Generated 2020-03-19  20:46:51</small></p></footer></body></html>