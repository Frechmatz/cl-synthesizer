<html><head></head><script type="text/javascript" src="toggledisplay.js"></script><body><header class="container"><h1 id="id-cl-synthesizer-patches">cl-synthesizer-patches</h1>Example patches for cl-synthesizer. Work in progress...<p>Back to the <a href="https://frechmatz.github.io/cl-synthesizer/">project site.</a></p></header><section class="container"><h1 id="id-<p><b>Siren: Two low-frequency modulated saw signals.</b></p>"><p><b>Siren: Two low-frequency modulated saw signals.</b></p></h1><audio controls="controls" preload="none">Your browser does not support the <code>audio</code> element.<source src="siren.wav?cb=3786733722" type="audio/wav"></audio><p><a href="#" id="G508"onclick="return toggleDisplay('Show patch', 'Hide patch', 'G508', 'G509')">Show patch</a></p><div style="display: none" id='G509'><p><pre><code>(defpackage :cl-synthesizer-patches-siren
  (:use :cl)
  (:documentation "Siren: Two low-frequency modulated saw signals."))

(in-package :cl-synthesizer-patches-siren)

(defun make-voice (name environment &amp;key lfo-frequency vco-frequency)
  "Frequency modulated saw"
  (declare (ignore name))
  (let ((voice
         (cl-synthesizer:make-rack
          :environment environment
          :output-sockets '(:audio))))
    
    (cl-synthesizer:add-module
     voice "LFO"
     #'cl-synthesizer-modules-vco:make-module
     :base-frequency lfo-frequency :v-peak 5.0)

    (cl-synthesizer:add-module
     voice "VCO"
     #'cl-synthesizer-modules-vco:make-module
     :base-frequency vco-frequency :v-peak 5.0 :cv-lin-hz-v 20.0)

    (cl-synthesizer:add-patch voice "LFO" :sine "VCO" :cv-lin)
    (cl-synthesizer:add-patch voice "VCO" :saw "OUTPUT" :audio)
    
    voice))
  
(defun example (&amp;key (sample-rate 44100))
  (let ((rack (cl-synthesizer:make-rack
               :environment (cl-synthesizer:make-environment :sample-rate sample-rate)
               :output-sockets '(:left :right))))

    (cl-synthesizer:add-module
     rack "VOICE-1" #'make-voice :lfo-frequency 1.0 :vco-frequency 440.0)
    (cl-synthesizer:add-module
     rack "VOICE-2" #'make-voice :lfo-frequency 2.0 :vco-frequency 442.0)

    (cl-synthesizer:add-patch rack "VOICE-1" :audio "OUTPUT" :left)
    (cl-synthesizer:add-patch rack "VOICE-2" :audio "OUTPUT" :right)

    (cl-synthesizer-monitor:add-monitor
     rack
     #'cl-synthesizer-monitor-wave-handler:make-handler
     '(("OUTPUT" :input-socket :left)
       ("OUTPUT" :input-socket :right))
     :filename "src/lisp/cl-synthesizer/docs/siren.wav")
    
    rack))

(defparameter *duration-seconds* 2.0)
(defparameter *audio-output-sockets* '(:left :right))

(defun run-example ()
  (cl-synthesizer:play-rack
   (example)
   :duration-seconds *duration-seconds*))
  
;;(run-example)</code></pre></p></div></section><footer class="container"><p><small>Generated 2019-12-30  23:28:42</small></p></footer></body></html>